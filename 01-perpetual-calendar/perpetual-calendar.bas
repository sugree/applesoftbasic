1    REM **********************
2    REM * PERPETUAL CALENDAR *
3    REM **********************
10   HOME : CLEAR
20   PRINT
30   DIM D(12),M$(12)
40   FOR I = 1 TO 12
50     READ D(I)
60   NEXT I
70   FOR I = 1 TO 12: READ M$(I): NEXT I
80   VTAB 1
90   FOR I = 1 TO 40: PRINT "-";: NEXT I
100  PRINT : HTAB (15): INPUT "YEAR(B.E.):";Y
110  PRINT
120  FOR I = 1 TO 40: PRINT "-";: NEXT I
130  IF Y = 0 AND M = 0 THEN END
140  Y = Y - 543
150  IF (Y - 1) * (Y - 3000) > 0 THEN PRINT : PRINT "ILLEGAL INPUT!!! PRESS ANY KEY";: GET Q$ : PRINT : HOME : GOTO 80
160  FOR M = 1 TO 12
170    J = 0
180    IF Y / 4 = INT (Y / 4) THEN J = J + 1
190    IF Y / 100 = INT (Y / 100) THEN J = J - 1
200    IF Y / 400 = INT (Y / 400) THEN J = J + 1
210    YP = Y - 1
220    YS = Y + INT (YP / 4) - INT (YP / 100) + INT (YP / 400)
230    YS = YS - INT (YS / 7) * 7
240    MS = 0
250    IF M = 1 THEN 310
260    FOR I = 1 TO M - 1
270      N = D(I)
280      IF I = 2 AND J = 1 THEN N = N + 1
290      MS = MS + N
300    NEXT I
310    MS = MS - INT (MS / 7) * 7
320    S = (YS + MS) - INT ((YS + MS) / 7) * 7
330    N = D(M)
340    IF M = 2 AND J = 1 THEN N = N + 1
350    POKE 34,15: POKE 35,21: HOME: POKE 34,0: POKE 35,24: VTAB 6
360    PRINT : PRINT : PRINT : PRINT
370    PRINT : PRINT TAB(8);M$(M); TAB(20);Y + 543: PRINT
380    LINE$ = "============================="
390    HEAD$ = "!SUN MON TUE WED THU FRI SAT!"
400    T = 8
410    PRINT TAB(T);LINE$: PRINT TAB(T);HEAD$: PRINT TAB(T);LINE$
420    FOR D = 1 TO N
430      PRINT TAB(4 * S + T +2);D;
440      S = S + 1
450      IF S < 7 THEN 480
460      PRINT
470      S = 0
480    NEXT D
490    PRINT
500    VTAB 22
510    PRINT TAB(T);LINE$
520    FOR I = 1 TO 150: VTAB 23: PRINT TAB(12)"PRESS <CTRL>S TO PAUSE": NEXT
530  NEXT M
540  VTAB 3: HTAB 26: PRINT " "
550  GOTO 80
560  DATA 31,28,31,30,31,30,31,31,30,31,30,31
570  DATA JANUARY,FEBRUARY,MARCH,APRIL,MAY,JUNE,JULY,AUGUST,SEPTEMBER,OCTOBER,NOVEMBER,DECEMBER
