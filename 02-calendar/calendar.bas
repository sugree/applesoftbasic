1    REM  ************
2    REM  * CALENDAR *
3    REM  ************
10   HOME
20   PRINT
30   DIM D(12),M$(12)
40   FOR I = 1 TO 12
50     READ D(I)
60   NEXT I
70   FOR I = 1 TO 12: READ M$(I): NEXT I
80   VTAB 1
90   FOR I = 1 TO 40: PRINT "-";: NEXT I
100  HTAB (14): VTAB (2): INPUT "MONTH(1-12):";M
110  PRINT : HTAB (15): INPUT "YEAR(B.E.):";Y
120  FOR I = 1 TO 40: PRINT "-";: NEXT I
130  IF Y = 0 AND M = 0 THEN END
140  Y = Y - 543
150  IF (Y - 1) * (Y - 3000) > 0 OR (M - 1) * (M - 12) > 0 THEN PRINT : PRINT "ILLEGAL INPUT!!! PRESS ANY KEY";: GET Q$ : PRINT : HOME : GOTO 80
160  J = 0
170  IF Y / 4 = INT (Y / 4) THEN J = J + 1
180  IF Y / 100 = INT (Y / 100) THEN J = J - 1
190  IF Y / 400 = INT (Y / 400) THEN J = J + 1
200  YP = Y - 1
210  YS = Y + INT (YP / 4) - INT (YP / 100) + INT (YP / 400)
220  YS = YS - INT (YS / 7) * 7
230  MS = 0
240  IF M = 1 THEN 300
250  FOR I = 1 TO M - 1
260    N = D(I)
270    IF I = 2 AND J = 1 THEN N = N + 1
280    MS = MS + N
290  NEXT I
300  MS = MS - INT (MS / 7) * 7
310  S = (YS + MS) - INT ((YS + MS) / 7) * 7
320  N = D(M)
330  IF M = 2 AND J = 1 THEN N = N + 1
340  POKE 34,15: POKE 35,21: HOME: POKE 34,0: POKE 35,24: VTAB 6
350  PRINT : PRINT : PRINT : PRINT
360  PRINT : PRINT TAB(8); M$(M); TAB(20); Y + 543 : PRINT
370  LINE$ = "============================="
380  HEAD$ = "!SUN MON TUE WED TUE FRI SAT!"
390  T = 8
400  PRINT TAB(T); LINE$ : PRINT TAB(T); HEAD$ : PRINT TAB(T); LINE$
410  FOR D = 1 TO N
420    PRINT TAB(4 * S + T + 2); D;
430    S = S + 1
440    IF S < 7 THEN 470
450    PRINT
460    S = 0
470  NEXT D
480  PRINT
490  VTAB 22
500  PRINT TAB(T); LINE$
510  VTAB 2: HTAB 26: PRINT " ": PRINT : HTAB 26: PRINT " "
520  GOTO 80
530  DATA 31,28,31,30,31,30,31,31,30,31,30,31
540  DATA JANUARY,FEBRUARY,MARCH,APRIL,MAY,JUNE,JULY,AUGUST,SEPTEMBER,OCTOBER,NOVEMBER,DECEMBER
